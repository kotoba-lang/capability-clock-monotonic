(ns kotoba.capability.clock.monotonic-test
  (:require [clojure.test :refer [deftest is]]
            [clojure.java.io :as io]
            [kotoba.capability.clock.monotonic :as capability]
            [kotoba.capability.clock.monotonic.provider :as provider]
            [kotoba.core.capability-repository :as repository]
            [kotoba.core.contracts :as contracts])
  (:import [java.security MessageDigest]))

(defn- sha256-file [f]
  (let [md (MessageDigest/getInstance "SHA-256")
        bytes (.digest md (.readAllBytes (io/input-stream f)))]
    (apply str (map #(format "%02x" (bit-and % 0xff)) bytes))))

(deftest manifest-conforms-as-reference-implemented
  (is (= :reference-implemented (:capability/provider-status capability/manifest)))
  (is (= "clock/monotonic" (:capability/id capability/manifest)))
  (is (= "bafyreiacwmejactezvrcbrxlqqyxha2l3qaw253tcfuwsywvzbajwo5fja"
         (:capability/definition-cid capability/manifest)))
  (is (= [] (repository/validate-manifest (contracts/capability-contract) capability/manifest))))

(deftest artifact-sha256-matches-bytes
  (let [path (io/file "artifacts/provider.core.wasm")
        declared (get-in capability/manifest [:capability/artifact :sha256])]
    (is (.isFile path))
    (is (= declared (sha256-file path)))))

(deftest artifact-exports-match-host-abi
  (let [exports (get-in capability/manifest [:capability/artifact :exports])
        abi (get-in capability/manifest [:capability/artifact :host-abi])]
    (is (= {"clock_monotonic" {:params [], :result :i64}} exports))
    (is (= {:module "kotoba", :field "clock_monotonic"} abi))))

(deftest jvm-reference-provider-is-monotonic
  (let [export (provider/host-export)
        f (:fn export)
        a (f)
        b (f)]
    (is (= "kotoba" (:module export)))
    (is (= "clock_monotonic" (:field export)))
    (is (= [] (:params export)))
    (is (= :i64 (:result export)))
    (is (number? a))
    (is (>= b a))))
